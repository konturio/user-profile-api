plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '3.3.0'
    id 'de.undercouch.download' version '5.1.2'
    id 'com.palantir.git-version' version '0.15.0'
}

group = 'io.kontur.userprofile'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'
def details = versionDetails()

repositories {
    mavenCentral()
    maven {
        url 'https://nexus.kontur.io:8084'
        credentials {
            username = System.getenv()['NEXUS_READER'] ?: "".trim()
            password = System.getenv()['NEXUS_READER_PASS'] ?: "".trim()
        }
    }
    maven {
        url 'https://nexus.kontur.io:8085'
        credentials {
            username = System.getenv()['NEXUS_DEPLOYER'] ?: "".trim()
            password = System.getenv()['NEXUS_DEPLOYER_PASS'] ?: "".trim()
        }
    }
}

dependencies {
    //model
    implementation project(":model")
    implementation 'org.wololo:jts2geojson:0.16.1'

    //json logging
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'

    //lombok
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    compileOnly 'org.projectlombok:lombok:1.18.22'

    //spring boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    //spring security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    //swagger
    implementation 'org.springdoc:springdoc-openapi-ui:1.6.2'

    //health
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.8.1'

    //liquibase
    implementation 'org.liquibase:liquibase-core:4.6.2'
    implementation 'org.liquibase.ext:liquibase-hibernate5:4.6.2'

    //tests
    implementation 'junit:junit:4.13.2'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.data:spring-data-jdbc'
    testImplementation 'org.testcontainers:postgresql:1.16.2'

    //postgresql
    implementation 'org.postgresql:postgresql:42.3.1'
}

checkstyle {
    configFile file("codestyle.xml")
}

test {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

jar {
    enabled = false
}

jacocoTestReport {
    reports {
        csv.enabled true
        csv.destination file("${buildDir}/jacoco.csv")
    }
}

jib {
    from {
        image = 'nexus.kontur.io:8084/library/openjdk:17-alpine'
        auth {
            username = System.getenv()['NEXUS_READER'] ?: "".trim()
            password = System.getenv()['NEXUS_READER_PASS'] ?: "".trim()
        }
    }
    to {
        image = "nexus.kontur.io:8085/konturdev/${project.name}:${project.version}"
        tags = ["latest", details.branchName + '.' + details.gitHash, details.lastTag + '.' + details.commitDistance + '.' + details.gitHash ]
        auth {
            username = System.getenv()['NEXUS_DEPLOYER'] ?: "".trim()
            password = System.getenv()['NEXUS_DEPLOYER_PASS'] ?: "".trim()
        }
    } //todo check
    container {
        mainClass 'io.kontur.userprofile.UserProfileServiceApplication'

        jvmFlags = ['-server', '-Xms256m', '-Xmx512m', '-XX:+HeapDumpOnOutOfMemoryError',
                    '-javaagent:/opentelemetry/opentelemetry-javaagent.jar',
                    '-XX:HeapDumpPath=/tmp/ups_oom.hprof', '-XX:MaxMetaspaceSize=384m']
        //if GC log is required: '-Xlog:gc*:file=/tmp/user-profile-api.GC.log'
    }
//    dockerClient { //todo use for local minikube
//        environment = ["DOCKER_TLS_VERIFY":System.env.DOCKER_TLS_VERIFY, "DOCKER_HOST":System.env.DOCKER_HOST,
//                       "DOCKER_CERT_PATH":System.env.DOCKER_CERT_PATH,
//                       "MINIKUBE_ACTIVE_DOCKERD":System.env.MINIKUBE_ACTIVE_DOCKERD]
//    }
    extraDirectories {
        paths {
            path {
                from = "${buildDir}/../jib/"
                into = '/opentelemetry/'
            }
        }
    }
}

task downloadFile(type: Download) {
    src 'https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases/download/v1.16.0/opentelemetry-javaagent.jar'
    dest "${buildDir}/../jib/opentelemetry-javaagent.jar"
    onlyIfModified true
}

task verifyFile(type: Verify, dependsOn: downloadFile) {
    dependsOn downloadFile
    src new File("${buildDir}/../jib/", 'opentelemetry-javaagent.jar')
    algorithm 'MD5'
    checksum '4f97ebd74d075e60c76ecdf47b61006a'
}

tasks.jib.dependsOn(verifyFile)
tasks.jibDockerBuild.dependsOn(verifyFile) //TODO can this be moved into the plugins block?
